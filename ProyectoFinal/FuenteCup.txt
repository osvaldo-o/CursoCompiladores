package fes.aragon.compilador;
import java_cup.runtime.*;
import java.io.*;
import java.util.*;
action code{:
	public StringBuilder info = new StringBuilder();
	public Archivo archivoSalida=
    new Archivo(new File(System.getProperty("user.dir")+"/salida.txt"));
    
	public void almacenar(String datos,boolean valor){
		info.append(datos + "\n");
		archivoSalida.escribir(info,valor);
	}
:};
parser code{:
	public static Lexico lex;
	public static String textError;
	public static boolean valido = true;
	
	public void syntax_error(Symbol st){
        textError = "Error en la linea "+ lex.getYyline() +
                    " columna: "+ lex.getYy_currentPos();
        valido = false;
	}
	
	public void cargar(InputStream archivo) throws Exception{	
		lex=new Lexico(archivo);
		new parser(lex).parse();
	}
:};

terminal PUNTOYCOMA, REPETIR, VER, FRUTA;
terminal COLOCA, MOVER, ARRIBA, ABAJO;
terminal IZQ, DER, PARA, PARC;
terminal Integer NUMERO;

non terminal instruccion,instrucciones,instruccion_s;
non terminal instrucciones_r,instruccion_r_s,instrucciones_v,instruccion_v_s;

instrucciones ::= instrucciones:ins_1 instruccion_s:ins_2 {:RESULT=new String(ins_1+"\n"+ins_2);:} 
			| instruccion_s:ins {:RESULT=ins;:}
			| error {:;:}
			;
			
instruccion ::= COLOCA NUMERO:x NUMERO:y {:RESULT="coloca "+x+" "+y;:}
			| MOVER NUMERO:n {:RESULT="mover "+n;:}
			| ARRIBA {:RESULT="arriba";:}
			| ABAJO {:RESULT="abajo";:}
			| IZQ {:RESULT="izquierda";:}
			| DER {:RESULT="derecha";:}
			;

instruccion_s ::= instruccion:ins PUNTOYCOMA {:almacenar(ins+"",false);:}
			| REPETIR NUMERO:n PARA instrucciones_r:ins PARC
			{:
				for (int i=0;i<n;i++){
					almacenar(ins+"",false);
				}
			:} 
			| VER FRUTA PARA instrucciones_v:ins PARC
			{:
				almacenar("ver fruta {",false);
				almacenar(ins+"",false);
				almacenar("}",false);
			:}
			;
			
instrucciones_r ::= instrucciones_r:ins_1 instruccion_r_s:ins_2 {:RESULT=new String(ins_1+"\n"+ins_2);:}
			| instruccion_r_s:ins {:RESULT=ins;:}
			;
			
instruccion_r_s ::= instruccion:ins PUNTOYCOMA {:RESULT= ins;:}
			| VER FRUTA PARA instrucciones_v:ins PARC
			{:
				RESULT="ver fruta {\n"+ins+"\n}";
			:}
			;
			
instrucciones_v ::= instrucciones_v:ins_1 instruccion_v_s:ins_2 {:RESULT=new String(ins_1+"\n"+ins_2);:}
			| instruccion_v_s:ins {:RESULT=ins;:}
			;
			
instruccion_v_s ::= instruccion:ins PUNTOYCOMA {:RESULT= ins;:}
			| REPETIR NUMERO:n PARA instrucciones_r:ins PARC 
			{:
				String dato = "";
				for (int i=0;i<n;i++){
					if (i<n-1){
						dato = dato + ins + "\n";
					}else{
						dato = dato + ins;
					}
				}
				RESULT=dato;
			:}
			;		